#Использовать InternetMail

#Область ПрограммныйИнтерфейс

Процедура ОтправитьУведомление(НастройкиТранспорта, ПараметрыУведомления) Экспорт
	
	Профиль = ПрофильПочты(НастройкиТранспорта);
	Сообщение = ИсходящееСообщение(НастройкиТранспорта, ПараметрыУведомления);
	
	Почта = Новый ИнтернетПочта;
	Почта.Подключиться(Профиль);
	Почта.Послать(Сообщение, , ПротоколИнтернетПочты.SMTP);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрофильПочты(НастройкиТранспорта)

	Профиль = Новый ИнтернетПочтовыйПрофиль;
	Профиль.Таймаут = 10;
	
	Профиль.АдресСервераSMTP = НастройкиТранспорта["Сервер"];
	Профиль.ПортSMTP = НастройкиТранспорта["Порт"];
	Профиль.ПользовательSMTP = НастройкиТранспорта["Логин"];
	Профиль.ПарольSMTP = НастройкиТранспорта["Пароль"];
	Профиль.ИспользоватьSSLSMTP = НастройкиТранспорта["ИспользоватьSSL"];
	
	Возврат Профиль;

КонецФункции

Функция ИсходящееСообщение(НастройкиТранспорта, ПараметрыУведомления)

	ТипТекста = Неопределено;
	Если ПараметрыУведомления["ТипТекста"] = Уведомления.ТипыТекста().html Тогда
		ТипТекста = ТипТекстаПочтовогоСообщения.HTML;
	КонецЕсли;

	Получатели = НастройкиТранспорта["Получатели"];
	Сообщение  = Новый ИнтернетПочтовоеСообщение;
	
	Для Каждого Получатель Из Получатели Цикл
		Сообщение.Получатели.Добавить(Получатель);
	КонецЦикла;
	
	Сообщение.Отправитель = НастройкиТранспорта["Отправитель"];
	Сообщение.Тема = ПараметрыУведомления["Тема"];
	
	Сообщение.Тексты.Добавить(ПараметрыУведомления["Текст"], ТипТекста);
	Сообщение.Вложения.Добавить(ПараметрыУведомления["Вложение"]);
	
	Возврат Сообщение;

КонецФункции

#КонецОбласти
