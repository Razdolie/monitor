#Использовать 1connector
#Использовать logos

Перем Лог;

Процедура ОтправитьУведомление(НастройкиТранспорта, ПараметрыУведомления) Экспорт
	
	Лог = Уведомления.Лог();

	Получатели = НастройкиТранспорта["Получатели"];
	Токен      = НастройкиТранспорта["Токен"];
	
	Тема    = ПараметрыУведомления["Тема"];
	Текст   = ПараметрыУведомления["Текст"];
	ЭтоHtml = ПараметрыУведомления["ТипТекста"] = Уведомления.ТипыТекста().html;
	
	Если ЭтоHtml Тогда
		Заголовок = СтрШаблон("<b>%1:</b>", Тема);
	Иначе
		Заголовок = СтрШаблон("**%1:**", Тема);
	КонецЕсли;

	Сообщение = Заголовок + Символы.ПС + Символы.ПС + Текст;

	АдресРесурса = "https://api.telegram.org/bot" + Токен + "/sendMessage";
	
	Для Каждого Получатель Из Получатели Цикл
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("chat_id", Получатель);
		ПараметрыЗапроса.Вставить("text", Сообщение);
		
		Если ЭтоHtml Тогда
			ПараметрыЗапроса.Вставить("parse_mode", "html");
		КонецЕсли;

		Ответ = КоннекторHTTP.Get(АдресРесурса, ПараметрыЗапроса);
		
		Если Ответ["КодСостояния"] <> 200 Тогда
			Лог.Ошибка("Не удалось отправить уведомление телеграм пользователю %1", Получатель);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры