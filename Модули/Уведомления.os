#Использовать logos

Перем Лог;

#Область ПрограмммныйИнтерфейс

Процедура ОтправитьУведомление(ИмяТранспорта, НастройкиТранспорта, ПараметрыУведомления) Экспорт
	
	Лог = Уведомления.Лог();
	
	Если Не НастройкиТранспорта["Использовать"] Тогда
		Лог.Отладка("Отправка уведомлений через ""%1"" отключена", ИмяТранспорта);
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Менеджер = МенеджерТранспорта(ИмяТранспорта);
		Менеджер.ОтправитьУведомление(НастройкиТранспорта, ПараметрыУведомления);
		
		Лог.Информация("Уведомление через ""%1"" отправлено", ИмяТранспорта);
		
	Исключение
		
		ТекстОшибки = "Ошибка отправки уведомления через ""%1"": "
			+ КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		Лог.Ошибка(ТекстОшибки, ИмяТранспорта);
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПараметрыУведомления() Экспорт
	
	Параметры = Новый Соответствие;
	Параметры.Вставить("Тема");
	Параметры.Вставить("Текст");
	Параметры.Вставить("Вложение");
	Параметры.Вставить("ТипТекста", ТипыТекста().html);
	
	Возврат Параметры;
	
КонецФункции

Функция ТипыТекста() Экспорт
	
	Типы = Новый Структура;
	Типы.Вставить("html", "html");
	Типы.Вставить("markdown", "markdown");
	
	Возврат Типы;
	
КонецФункции


Функция Лог() Экспорт
	
	ИмяЛога = ПараметрыПриложения.ИмяПриложения() + ".notification";
	
	Если Лог = Неопределено Тогда
		Лог = Логирование.ПолучитьЛог(ИмяЛога);
	КонецЕсли;
	
	Возврат Лог;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МенеджерТранспорта(ИмяТранспорта)
	
	Менеджеры = Новый Соответствие;
	Менеджеры.Вставить("Почта", УведомленияПочта);
	Менеджеры.Вставить("Телеграм", УведомленияТелеграм);
	
	Возврат Менеджеры[ИмяТранспорта];
	
КонецФункции

#КонецОбласти